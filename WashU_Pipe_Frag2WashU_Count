#!/usr/bin/perl

use strict;
use warnings;
use POSIX; 
use Getopt::Long;
use FindBin '$Bin';
use lib $Bin;
use WashU_Pipe_Module;

use Data::Dumper;

###################################################################################
###################################################################################
##This file is Copyright (C) 2015, Steven Wingett (steven.wingett@babraham.ac.uk)##
##                                                                               ##
##                                                                               ##
##This file is part of WashU_Pipe.                                               ##
##                                                                               ##
##WashU_Pipe is free software: you can redistribute it and/or modify             ##
##it under the terms of the GNU General Public License as published by           ##
##the Free Software Foundation, either version 3 of the License, or              ##
##(at your option) any later version.                                            ##
##                                                                               ##
##WashU_Pipe is distributed in the hope that it will be useful,                  ##
##but WITHOUT ANY WARRANTY; without even the implied warranty of                 ##
##MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the                  ##
##GNU General Public License for more details.                                   ##
##                                                                               ##
##You should have received a copy of the GNU General Public License              ##
##along with WashU_Pipe.  If not, see <http://www.gnu.org/licenses/>.            ##
###################################################################################
###################################################################################



###############################################################################################
#Perl script to convert a restriction fragment coordinates file
#a format that can be read by the WashU browser
#WashU browser format:
#chr1   \t   111   \t   222   \t   chr2:333-444,55   \t   1   \t   .
#chr2   \t   333   \t   444   \t   chr1:111-222,55   \t   2   \t   .
#
#After running this script
#1) Import ucsc_tools and tabix
#2) bedSort filein fileout
#3) bgzip bedfile
#4) tabix -p bed gedfile.gz
#5)Upload the tbi file and the bedfile.gz to the remote server.  The http link needs to be
#passed to the washU browser.
###############################################################################################

my $version;
my $filelist;

my $config_result = GetOptions(
	"filelist=s" => \$filelist,
	"version" => \$version
);
die "Could not parse options.\n" unless ($config_result);

if($version){
	$version = get_version();
	print "WashU_Pipe v$version\n";
	exit(0);
}

my @files;
push(@files, getFilenames($filelist)) if defined $filelist;
push (@files, @ARGV) if @ARGV;
@files = deduplicate_array(@files);
die "Please specify files to process.\n" unless (@files);


foreach my $file (@files){

	if ($file =~ /\.bam$/){
		open (IN,"samtools view -h $file |") or die "Could not open '$file' : $!";
	}else{
		open(IN, '<', $file) or die "Could not open '$file' : $!";
	}	

	print "Converting fragments data to WashU format (counting interactions) in file '$file'\n";
	
	#Get chromosome length to determine end point of last bin (else the last bin may extend beyond
	#the end of the chromosome.
	my %interactions;    #Interaction - count
	
	print "Processing $file\n";
	

	while(<IN>){
		chomp;
		my $readF = $_;
		my $readR = scalar <IN>;
		chomp $readR;
		
		my $chromosomeF = (split(/\t/, $readF))[0];
		my $chromosomeR = (split(/\t/, $readR))[0];
		my $startF = (split(/\t/, $readF))[1];
		my $startR = (split(/\t/, $readR))[1];
		my $endF = (split(/\t/, $readF))[2];
		my $endR = (split(/\t/, $readR))[2];

		
		#Which read of the read pair is sequenced first is random (i.e. expected to be 50:50), 
		#and is not of biological significance, so store in the interactions hash accordingly
		my $labelF = "$chromosomeF\t$startF\t$endF";
		my $labelR = "$chromosomeR\t$startR\t$endR";
	
		if(($labelF cmp $labelR) == 1){
			$interactions{"$labelR\t$labelF"}++;
		}else{
			$interactions{"$labelF\t$labelR"}++;
		}	
	}	
	
	close IN;

	open(OUT, '>', "$file.washu_format.txt") or die "Could not open '$file.washu_format.txt' : $!";
	my $i = 1;
	foreach my $interaction (keys %interactions){
		my ($csomeF, $startF, $endF, $csomeR, $startR, $endR) = split(/\t/, $interaction);
		my $count = $interactions{$interaction};

		$csomeF = csome_formatter($csomeF);
		$csomeR = csome_formatter($csomeR);
		
		print OUT "$csomeF\t$startF\t$endF\t".$csomeR.':'.$startR.'-'.$endR.','."$count\t$i\t.\n";
		$i++;
		print OUT "$csomeR\t$startR\t$endR\t".$csomeF.':'.$startF.'-'.$endF.','."$count\t$i\t.\n";
		$i++;
	}

	close OUT;

}

print "Conversion complete.\n";